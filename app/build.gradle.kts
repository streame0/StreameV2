import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    // Kotlin 2.0+: Compose compiler is its own plugin, not a
    // `composeOptions.kotlinCompilerExtensionVersion` pin. Version tracks
    // Kotlin in the root build.gradle.kts.
    id("org.jetbrains.kotlin.plugin.compose")
    id("com.google.devtools.ksp")
    id("com.google.dagger.hilt.android")
    id("org.jetbrains.kotlin.plugin.serialization")
    id("androidx.baselineprofile")
    id("com.google.android.libraries.mapsplatform.secrets-gradle-plugin")
    id("io.gitlab.arturbosch.detekt")
    // Firebase Crashlytics - uncomment after adding google-services.json
    // id("com.google.gms.google-services")
    // id("com.google.firebase.crashlytics")
}

android {
    namespace = "com.streame.tv"
    compileSdk = 35

    flavorDimensions += "distribution"

    defaultConfig {
        applicationId = "com.streame.tv"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        // Fire TV devices can be as low as Android 7.1 (API 25) or lower depending on model/OS.
        // Lower minSdk to maximize compatibility and avoid "There was a problem parsing the package".
        minSdk = 21
        targetSdk = 35
        // Force Hilt Application classes into the primary DEX so they're
        // available during the very first moments of process startup.
        multiDexKeepProguard = file("multidex-config.pro")
        versionCode = 3
        versionName = "1.3"
        buildConfigField("String", "GITHUB_OWNER", "\"streame0\"")
        buildConfigField("String", "GITHUB_REPO", "\"StreameV2\"")
        buildConfigField("String", "SUPABASE_URL", "\"${localSecretValue("SUPABASE_URL")}\"")
        buildConfigField("String", "SUPABASE_ANON_KEY", "\"${localSecretValue("SUPABASE_ANON_KEY")}\"")
        buildConfigField("String", "TV_LOGIN_WEB_BASE_URL", "\"${localSecretValue("TV_LOGIN_WEB_BASE_URL")}\"")

        // Support both 32-bit and 64-bit devices
        ndk {
            abiFilters += listOf("armeabi-v7a", "arm64-v8a", "x86", "x86_64")
        }

        vectorDrawables {
            useSupportLibrary = true
        }

        // Enable R8 full mode for better optimization
        proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"))
    }

    productFlavors {
        create("sideload") {
            dimension = "distribution"
        }
    }

    // Release signing configuration
    // To set up: create keystore.properties in project root with:
    //   storeFile=path/to/your.keystore
    //   storePassword=your_store_password
    //   keyAlias=your_key_alias
    //   keyPassword=your_key_password
    signingConfigs {
        create("release") {
            val keystorePropertiesFile = rootProject.file("keystore.properties")
            if (keystorePropertiesFile.exists()) {
                val keystoreProperties = Properties()
                keystoreProperties.load(keystorePropertiesFile.inputStream())
                storeFile = file(keystoreProperties["storeFile"] as String)
                storePassword = keystoreProperties["storePassword"] as String
                keyAlias = keystoreProperties["keyAlias"] as String
                keyPassword = keystoreProperties["keyPassword"] as String
            }
        }
    }

    buildTypes {
        release {
            // Full release optimization for TV smoothness.
            isMinifyEnabled = true
            isShrinkResources = true
            // Use release signing if configured, otherwise fall back to debug
            val releaseSigningConfig = signingConfigs.findByName("release")
            signingConfig = if (releaseSigningConfig?.storeFile != null) {
                releaseSigningConfig
            } else {
                signingConfigs.getByName("debug")
            }
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )

            // Optimization flags
            isDebuggable = false
            isJniDebuggable = false
            renderscriptOptimLevel = 3

            // Build config fields for release
            buildConfigField("Boolean", "ENABLE_CRASH_REPORTING", "true")
            buildConfigField("Boolean", "ENABLE_ANALYTICS", "true")
        }

        debug {
            isMinifyEnabled = false
            isDebuggable = true
            // applicationIdSuffix = ".debug" // Disabled to preserve settings between debug/release
            versionNameSuffix = "-debug"

            // Build config fields for debug
            buildConfigField("Boolean", "ENABLE_CRASH_REPORTING", "false")
            buildConfigField("Boolean", "ENABLE_ANALYTICS", "false")
        }

        // Staging build type: release-grade optimizations but signed with the
        // debug keystore so the APK installs as an update over an existing
        // debug build (preserves profile/DataStore). NO applicationId
        // suffix — it MUST resolve to the same package as debug/release.
        create("staging") {
            initWith(getByName("release"))
            versionNameSuffix = "-rc"
            signingConfig = signingConfigs.getByName("debug")
            isDebuggable = false
            isJniDebuggable = false

            buildConfigField("Boolean", "ENABLE_CRASH_REPORTING", "true")
            buildConfigField("Boolean", "ENABLE_ANALYTICS", "false")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        isCoreLibraryDesugaringEnabled = true
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    packaging {
        resources {
            excludes += setOf(
                "/META-INF/{AL2.0,LGPL2.1}",
                "/META-INF/LICENSE*",
                "/META-INF/NOTICE*",
            )
        }
        jniLibs {
            useLegacyPackaging = false  // Required for 16KB page size support
        }
    }

    baselineProfile {
        mergeIntoMain = true
    }
}

// Kotlin 2.0+ Compose compiler plugin config. The stability config file
// is the same as before — marks domain models as stable to avoid
// unnecessary recompositions — but fed through a first-class plugin
// extension instead of a raw -P freeCompilerArg.
composeCompiler {
    stabilityConfigurationFile = rootProject.layout.projectDirectory
        .file("app/compose_stability_config.conf")
}

// KSP configuration for Hilt
ksp {
    arg("dagger.fastInit", "enabled")
    arg("dagger.formatGeneratedSource", "disabled")
    arg("dagger.hilt.android.internal.disableAndroidSuperclassValidation", "true")
    // Room schema export — enables migration testing and validation
    arg("room.schemaLocation", "$projectDir/schemas")
}

dependencies {
    // Core library desugaring for Java 8+ APIs
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.4")

    // Core Android
    implementation("androidx.core:core-ktx:1.12.0")
    implementation("androidx.core:core-splashscreen:1.0.1")  // Android 12+ Splash Screen
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.7.0")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0")
    // Provides collectAsStateWithLifecycle — pauses Flow collection while the
    // screen is off so we don't drive recompositions on invisible UI.
    implementation("androidx.lifecycle:lifecycle-runtime-compose:2.7.0")
    implementation("androidx.activity:activity-compose:1.8.2")

    // Compose BOM — bumped alongside Kotlin 2.1. Staying on the 2024.06
    // line keeps tv-foundation 1.0.0-alpha11 happy; newer BOMs drift the
    // runtime off alpha11 and cause invalid-slot-table crashes on D-pad.
    implementation(platform("androidx.compose:compose-bom:2024.06.00"))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-graphics")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.material:material-icons-extended")
    implementation("androidx.compose.animation:animation")

    // Compose for TV - Core TV components
    // tv-foundation stays alpha (no beta/stable releases exist); tv-material bumped to stable
    implementation("androidx.tv:tv-foundation:1.0.0-alpha11")
    implementation("androidx.tv:tv-material:1.0.0")

    // Navigation
    implementation("androidx.navigation:navigation-compose:2.7.6")

    // Hilt for DI — 2.54 is the first release with Kotlin 2.1 metadata
    // support on the Java compile side. 2.52 fails on `hiltJavaCompile*`
    // with "Unable to read Kotlin metadata due to unsupported metadata
    // version" because Hilt parses generated `@Module` classes that carry
    // Kotlin 2.1's newer metadata format.
    implementation("com.google.dagger:hilt-android:2.54")
    ksp("com.google.dagger:hilt-compiler:2.54")
    implementation("androidx.hilt:hilt-navigation-compose:1.1.0")

    // Leanback (TV compliance, browse fragments if needed)
    implementation("androidx.leanback:leanback:1.1.0-rc02")
    implementation("androidx.tvprovider:tvprovider:1.1.0")

    // ExoPlayer / Media3 for video playback - version 1.3.1 for latest codec support
    val media3Version = "1.3.1"
    implementation("androidx.media3:media3-exoplayer:$media3Version")
    implementation("androidx.media3:media3-exoplayer-hls:$media3Version")
    implementation("androidx.media3:media3-exoplayer-dash:$media3Version")
    implementation("androidx.media3:media3-datasource-okhttp:$media3Version")
    implementation("androidx.media3:media3-ui:$media3Version")
    implementation("androidx.media3:media3-session:$media3Version")
    implementation("androidx.media3:media3-common:$media3Version")
    // FFmpeg extension for software decoding of DTS/TrueHD/Atmos/HEVC/DV.
    implementation("org.jellyfin.media3:media3-ffmpeg-decoder:1.3.1+2")

    // Networking - Retrofit + OkHttp
    implementation("com.squareup.retrofit2:retrofit:2.9.0")
    implementation("com.squareup.retrofit2:converter-gson:2.9.0")
    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    implementation("com.squareup.okhttp3:okhttp-dnsoverhttps:4.12.0")
    implementation("com.squareup.okhttp3:logging-interceptor:4.12.0")

    // CloudStream plugin runtime dependencies. NiceHttp (the HTTP client
    // real CloudStream providers import as `com.lagradost.nicehttp.Requests`)
    // is re-implemented in-tree at app/src/sideload/kotlin/com/lagradost/
    // nicehttp/ over the existing OkHttp dependency — upstream NiceHttp
    // binaries ship pre-compiled against newer Kotlin stdlibs than this
    // project supports. Jackson is used by MainAPIKt.getMapper(); jsoup
    // parses provider HTML for simple plugins.
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:2.15.3")
    implementation("org.jsoup:jsoup:1.17.2")
    implementation("me.xdrop:fuzzywuzzy:1.4.0")

    // Coroutines
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3")

    // Image loading - Coil 3.x
    implementation("io.coil-kt.coil3:coil:3.0.0")
    implementation("io.coil-kt.coil3:coil-compose:3.0.0")
    implementation("io.coil-kt.coil3:coil-gif:3.0.0")
    implementation("io.coil-kt.coil3:coil-svg:3.0.0")
    implementation("io.coil-kt.coil3:coil-network-okhttp:3.0.0")
    implementation("com.google.zxing:core:3.5.3")

    implementation("org.conscrypt:conscrypt-android:2.5.2")

    // DataStore for preferences
    implementation("androidx.datastore:datastore-preferences:1.0.0")

    // Room database (local-first storage)
    implementation("androidx.room:room-runtime:2.6.1")
    implementation("androidx.room:room-ktx:2.6.1") // Flow/Coroutine support
    ksp("androidx.room:room-compiler:2.6.1")


    // WorkManager for background sync
    implementation("androidx.work:work-runtime-ktx:2.10.0")
    implementation("androidx.hilt:hilt-work:1.1.0")
    ksp("androidx.hilt:hilt-compiler:1.1.0")

    // Profile installer for baseline profiles
    implementation("androidx.profileinstaller:profileinstaller:1.3.1")

    // Performance instrumentation
    implementation("androidx.metrics:metrics-performance:1.0.0-alpha04")
    implementation("androidx.tracing:tracing-ktx:1.2.0")

    // Debug tools
    debugImplementation("androidx.compose.ui:ui-tooling")
    debugImplementation("androidx.compose.ui:ui-test-manifest")

    // Firebase Crashlytics - optional, works when google-services.json is present
    implementation(platform("com.google.firebase:firebase-bom:32.7.0"))
    implementation("com.google.firebase:firebase-crashlytics-ktx")
    implementation("com.google.firebase:firebase-analytics-ktx")

    // Sentry crash reporting. Runtime initialization is gated by BuildConfig.ENABLE_CRASH_REPORTING
    // and SENTRY_DSN from secrets.properties/secrets.defaults.properties.
    implementation("io.sentry:sentry-android:8.40.0")

    // Supabase — cloud sync + auth
    implementation(platform("io.github.jan-tennert.supabase:bom:3.1.4"))
    implementation("io.github.jan-tennert.supabase:auth-kt")
    implementation("io.github.jan-tennert.supabase:postgrest-kt")
    implementation("io.github.jan-tennert.supabase:realtime-kt")
    implementation("io.ktor:ktor-client-okhttp:3.1.1")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.3")
    implementation("com.russhwolf:multiplatform-settings:1.3.0")

    // AndroidX Security — encrypted shared preferences for auth tokens
    implementation("androidx.security:security-crypto:1.1.0-alpha06")

    baselineProfile(project(":benchmark"))

    // Unit Testing
    testImplementation("junit:junit:4.13.2")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3")
    testImplementation("io.mockk:mockk:1.13.8")
    testImplementation("app.cash.turbine:turbine:1.0.0")  // Flow testing
    testImplementation("com.google.truth:truth:1.1.5")    // Better assertions
    testImplementation("org.robolectric:robolectric:4.11.1")  // Android mocking
    testImplementation("androidx.room:room-testing:2.6.1")
    testImplementation("androidx.datastore:datastore-preferences-core:1.0.0")

    // Android Instrumented Testing
    androidTestImplementation(platform("androidx.compose:compose-bom:2024.06.00"))
    androidTestImplementation("androidx.test.ext:junit:1.1.5")
    androidTestImplementation("androidx.test:core-ktx:1.5.0")
    androidTestImplementation("androidx.test:runner:1.5.2")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.1")
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
    androidTestImplementation("androidx.test.uiautomator:uiautomator:2.2.0")
    androidTestImplementation("io.mockk:mockk-android:1.13.8")
    androidTestImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3")
}

secrets {
    // Secrets file to read from
    propertiesFileName = "secrets.properties"

    // Default file with placeholder values (for CI/new developers)
    defaultPropertiesFileName = "secrets.defaults.properties"

    // Ignore missing keys to allow builds without secrets file
    ignoreList.add("sdk.*")
}

fun localSecretValue(name: String): String {
    val secretsFile = rootProject.file("secrets.properties")
    if (secretsFile.exists()) {
        val properties = Properties()
        secretsFile.inputStream().use { properties.load(it) }
        properties.getProperty(name)?.trim()?.takeIf { it.isNotBlank() }?.let { return it }
    }
    providers.gradleProperty(name).orNull?.trim()?.takeIf { it.isNotBlank() }?.let { return it }
    providers.environmentVariable(name).orNull?.trim()?.takeIf { it.isNotBlank() }?.let { return it }
    return ""
}


detekt {
    // Configuration file
    config.setFrom(files("$rootDir/config/detekt/detekt.yml"))

    // Baseline file for existing issues (generated with ./gradlew detektBaseline)
    baseline = file("$rootDir/config/detekt/baseline.xml")

    // Build upon default ruleset
    buildUponDefaultConfig = true

    // Run detekt on all source sets
    source.setFrom(
        files(
            "src/main/kotlin",
            "src/main/java"
        )
    )

    // Parallel execution
    parallel = true

    // Don't fail build on issues (use baseline instead)
    ignoreFailures = true
}
